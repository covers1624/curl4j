plugins {
    id 'java-library'
    id 'maven-publish'
    id 'c'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
}

group 'net.covers1624'
version '1.0-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    compileOnly 'org.jetbrains:annotations:24.0.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

model {
    components {
        curl4j(NativeLibrarySpec) {
            targetPlatform "windows_x86_64"
            targetPlatform "linux_x86_64"
            targetPlatform "linux_arm64"
            targetPlatform "macos_x86_64"
            targetPlatform "macos_arm64"

            sources {
                c {
                    source {
                        srcDir 'src/main/c'
                        include '**/*.c'
                    }
                    exportedHeaders {
                        srcDir 'src/generated/c'
                        srcDir "${System.properties['java.home']}/../include"
                        srcDir "${System.properties['java.home']}/../include/linux"
                        srcDir "${System.properties['java.home']}/../include/darwin"
                        srcDir "${System.properties['java.home']}/../include/win32"
                        srcDir "thirdparty/libffi/build_/include"
                    }
                }
            }
            binaries {
                all {
                    linker.args "-L${projectDir}/thirdparty/libffi/build_/.libs", "-lffi"
                }
            }
        }
    }

    platforms {
        windows_x86_64 {
            architecture "x86_64"
        }
        linux_x86_64 {
            architecture "x86_64"
        }
        linux_arm64 {
            architecture "arm64"
        }
        macos_x86_64 {
            architecture "x86_64"
        }
        macos_arm64 {
            architecture "arm64"
        }
    }

    buildTypes {
        debug
        release
    }
}

test {
    useJUnitPlatform()
}

compileJava {
    options.compilerArgs += ['-h', file('src/generated/c')]
}

tasks.withType(CCompile) {
    it.dependsOn('compileLibFFI')
}

tasks.register('compileLibFFI') {
    doFirst {
        def dir = file('thirdparty/libffi')
        def buildDir = cleanDir(new File(dir, 'build_'))
        runIn(dir, "./autogen.sh")
        runIn(buildDir, '../configure', '--enable-static', '--disable-shared', '--with-pic')
        runIn(buildDir, 'make')
    }
}

tasks.register('compileNative') {
    doFirst {
        def dir = file('build/native/')
        delete(dir)
        dir.mkdirs()
        exec {
            commandLine(['cmake', '../..'])
            workingDir(dir)
        }
        exec {
            commandLine("make")
            workingDir(dir)
        }
    }
}

publishing {
    repositories {
        if (System.getenv('MAVEN_PASS')) {
            maven {
                url "https://nexus.covers1624.net/repository/maven-snapshots/"
                credentials {
                    username 'covers1624'
                    password System.getenv('MAVEN_PASS')
                }
            }
        }
    }
    publications {
        publication(MavenPublication) {
            groupId rootProject.group
            artifactId project.archivesBaseName
            version rootProject.version

            from components.java
        }
    }
}


File cleanDir(File dir) {
    delete(dir)
    dir.mkdirs()
    return dir
}

def runIn(File dir, Object... args) {
    exec {
        commandLine(args)
        workingDir(dir)
    }
}
